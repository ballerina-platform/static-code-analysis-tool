/*
 * Copyright (c) 2024, WSO2 LLC. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

tasks.register('updateVersionScanReport', Exec) {
    doFirst {
        println "update version to " + version
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine 'cmd', '/c', 'npm', 'run', 'update-version', '--', version
        } else {
            commandLine 'npm', 'run', 'update-version', '--', version
        }
    }
}

tasks.register('npmInstallScanReport', Exec) {
    dependsOn updateVersionScanReport
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'npm', 'install'
    } else {
        commandLine 'npm', 'install'
    }
}

tasks.register('npmBuildScanReport', Exec) {
    dependsOn npmInstallScanReport
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'npm', 'run', 'build'
    } else {
        commandLine 'npm', 'run', 'build'
    }
}

tasks.register('createScanReportZip', Zip) {
    dependsOn npmBuildScanReport
    from "build"
    include 'index.html'
    include 'favicon.ico'
    include 'manifest.json'
    include '**/static/css/*.css'
    include '**/static/js/*.js'
    include '**/static/js/*.js.map'
    exclude '**/static/js/runtime-*.js'
    archiveFileName = 'report.zip'
    destinationDirectory = file("${rootDir}/scan-command/src/main/resources/")
}
